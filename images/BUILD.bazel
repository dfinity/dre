# Rules to build container images for application shipped by this
# repository.
#
# Note that some applications still aren't using images built this way.
#
# To update the image to a later snapshot:
#
# 1. Update the image snapshot digest in /MODULE.bazel with the latest
#    digest available on https://hub.docker.com/_/ubuntu/ for the
#    respective tag (Ubuntu 24.04 is so-called `noble`).
# 2. Update the date in the snapshot directory in the YAML file
#    corresponding to the image being updated in /MODULE.bazel.
# 3. Re-lock the lock file for the respective YAML file, e.g.
#    `bazel run @ubuntu_24_04_packages//:lock` at the root of the repo
#
# With these steps, an up-to-date container image will be used by
# downstream consumers.

load("@rules_oci//oci:defs.bzl", "oci_image")
load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@rules_distroless//distroless:defs.bzl", "group", "passwd", "cacerts", "home", "flatten")

package(default_visibility = ["//visibility:public"])

COMPATIBLE_WITH = select({
    "@platforms//cpu:x86_64": ["@platforms//cpu:x86_64"],
    "@platforms//cpu:arm64": ["@platforms//cpu:arm64"],
}) + [
    "@platforms//os:linux",
]

passwd(
    name = "passwd",
    entries = [
        {
            "uid": 0,
            "gid": 0,
            "home": "/root",
            "shell": "/bin/bash",
            "username": "root",
        },
        {
            "uid": 100,
            "gid": 65534,
            "home": "/home/_apt",
            "shell": "/usr/sbin/nologin",
            "username": "_apt",
        },
        {
            "uid": 1000,
            "gid": 1000,
            "home": "/home/user",
            "shell": "/bin/bash",
            "username": "user",
        },
    ],
)

group(
    name = "group",
    entries = [
        {
            "name": "root",
            "gid": 0,
        },
        {
            "name": "_apt",
            "gid": 65534,
        },
    ],
)

home(
    name = "home",
    dirs = [
        {
            "home": "/root",
            "uid": 0,
            "gid": 0,
        },
        {
            "home": "/home/user",
            "uid": 1000,
            "gid": 1000,
        },
    ],
)

# symlinks
tar(
    name = "symlinks",
    srcs = ["containers.conf", "sudoers", "pam_sudo_fix"],
    mtree = [
        # clang-symlinks
        # needed as dpkg assumes sh is installed in a typical debian installation.
        "./usr/bin/clang type=link link=/usr/bin/clang-20",
        "./usr/bin/asan_symbolize-20 type=link link=/usr/bin/asan_symbolize",
        "./usr/bin/clang++ type=link link=/usr/bin/clang++-20",
        "./usr/bin/clang-cpp type=link link=/usr/bin/clang-cpp-20",

        # podman symlinks
        "./usr/bin/podman type=link link=/usr/local/bin/podman",
        "./etc/containers/containers.conf type=file content=$(location containers.conf)",

        # iptables symlinks
        "./usr/bin/iptables type=link link=/usr/sbin/iptables-nft",
        "./usr/bin/iptables-save type=link link=/usr/sbin/iptables-nft-save",
        "./usr/bin/iptables-restore type=link link=/usr/sbin/iptables-nft-restore",

        # ip6tables symlinks
        "./usr/bin/ip6tables type=link link=/usr/sbin/ip6tables-nft",
        "./usr/bin/ip6tables-save type=link link=/usr/sbin/ip6tables-nft-save",
        "./usr/bin/ip6tables-restore type=link link=/usr/sbin/ip6tables-nft-restore",

        # sudoers for container run only
        "./etc/sudoers.d/podman_only type=file mode=0440 uid=0 gid=0 content=$(location sudoers)",

        # pam fix
        "./etc/pam.d/sudo type=file mode=0644 uid=0 gid=0 content=$(location pam_sudo_fix)",

        # which fix
        "./usr/bin/which type=link link=/usr/bin/which.debianutils",
    ],
)

cacerts(
    name = "cacerts",
    package = select({
        "@platforms//cpu:arm64": "@ubuntu_24_04_packages//ca-certificates/arm64:data",
        "@platforms//cpu:x86_64": "@ubuntu_24_04_packages//ca-certificates/amd64:data",
    }),
)

# The following thing is necessary because Bazel-wrapped Python programs
# insist on ignoring the default /usr/lib/ssl/cert.pem and /etc/ssl/certificates/...
# root and intermediate certificate store, making `urllib.request.urlopen()` fail
# for any Bazel-wrapped py_binary in the container.  This does not happen when
# regular Python is invoked inside of the container.
tar(
    name = "cacerts-symlink",
    mtree = [
        "./etc/ssl/cert.pem type=link link=/etc/ssl/certs/ca-certificates.crt",
    ],
)

flatten(
    name = "ubuntu_apk_flat",
    tars = [
        "@ubuntu_24_04_packages//:ubuntu_24_04_packages",
    ],
    deduplicate = True,
)

alias(
    name = "podman_layer",
    actual = select({
        "@platforms//cpu:arm64": "@podman_arm64//:layer",
        "@platforms//cpu:x86_64": "@podman_amd64//:layer",
    }),
)

oci_image(
    name = "ubuntu_24_04",
    architecture = select({
        "@platforms//cpu:arm64": "arm64",
        "@platforms//cpu:x86_64": "amd64",
    }),
    os = "linux",
    # NOTE: this is needed because, otherwise, bazel test //... fails, even
    # when container_structure_test already has target_compatible_with.
    # See 136
    target_compatible_with = COMPATIBLE_WITH,
    tars = [
        ":passwd",
        ":group",
        ":home",
        ":cacerts",
        ":cacerts-symlink",
        # Packages listed in this manifest (see YAML files in this folder)
        # get installed in the container by this rule.
        ":ubuntu_apk_flat",
        "symlinks",
        ":podman_layer",
    ],
)
