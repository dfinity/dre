load("@python_deps//:requirements.bzl", "requirement")
load("@rules_python//python:defs.bzl", "py_binary")
load("@//tools/python:py_oci_image.bzl", "py_oci_image")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@aspect_bazel_lib//lib:tar.bzl", "tar")

deps = [
    requirement("requests"),
    requirement("pydrive2"),
    requirement("markdown"),
    requirement("markdownify"),
    requirement("pymdown-extensions"),
    requirement("pytest"),
    requirement("pygithub"),
    requirement("pytest-mock"),
    requirement("mammoth"),
    requirement("slack-sdk"),
    requirement("python-dotenv"),
    requirement("pydiscourse"),
    requirement("pydantic-yaml"),
    requirement("ic-py"),
    requirement("tenacity"),
]

dev_deps = [
    requirement("httpretty"),
    requirement("pytest-xdist"),
    requirement("types-requests"),
    requirement("mypy"),
]

env = {
    "BAZEL": "true",
}

py_binary(
    name = "release-controller",
    main = "reconciler.py",
    srcs = glob(["*.py"], exclude=["commit_annotator.py"]),
    tags = ["typecheck"],
    env = {
        "DRE_PATH": "$(rootpath //rs/cli:dre-embedded)",
    },
    deps = deps + [
        requirement("prometheus-client"),
    ],
    data = ["//rs/cli:dre-embedded"] + [":bazelisk", ":target_determinator"],
)

py_binary(
    name = "commit-annotator",
    main = "commit_annotator.py",
    srcs = ["commit_annotator.py", "commit_annotation.py", "git_repo.py", "const.py", "release_index.py", "util.py", "watchdog.py"],
    tags = ["typecheck"],
    data = [":bazelisk", ":target_determinator"],
    env = env,
    deps = deps + [
        requirement("prometheus-client"),
    ],
)

py_binary(
    name = "release-notes",
    main = "release_notes.py",
    srcs = ["release_notes.py", "git_repo.py", "const.py", "release_index.py", "util.py", "commit_annotation.py", "watchdog.py"],
    tags = ["typecheck"],
    data = [":bazelisk", ":target_determinator"],
    env = env,
    deps = deps + [
        requirement("prometheus-client"),
    ]
)

native_binary(
    name = "bazelisk",
    src = select({
        "@platforms//os:linux": "@bazelisk_linux//file",
    }),
    out = "bazel",
)

native_binary(
    name = "target_determinator",
    src = select({
        "@platforms//os:linux": "@target_determinator//file",
    }),
    out = "target-determinator",
)

genrule(
    name = "ic_repo_clone_for_long_tests",
    outs = ["ic.git.tar"],
    cmd = "git clone https://github.com/dfinity/ic.git ic.git && cd `dirname $@`/ic.git && tar cf ../ic.git.tar .",
    tags = ["no-sandbox"] + ["manual"],
)

medium_tests = ["tests/test_release_notes.py", "tests/test_reconciler.py"]

# Add file here if you do not want to run the test automatically.
long_tests = ["tests/test_commit_annotator.py"]

[
    [
        py_test(
            name = target[6:-3],
            srcs = ["tests/runner.py"] + [target] + glob(["tests/mock_*.py"]),
            data = glob(["tests/test_data/*"]) + ["//:pyproject.toml"] + group["data"],
            main = "tests/runner.py",
            env = env | group["env"],
            tags = ["no-sandbox"] + group["tags"] + ["typecheck"],
            deps = deps + dev_deps + ["release-controller"] + ["commit-annotator"],
            env_inherit = ["HOME"],
            size = group["test_size"],
        )
        for target in group["targets"]
    ]
    for group in [
        {
            "targets": glob(["tests/test_*.py"], exclude=long_tests + medium_tests),
            "tags": [],
            "test_size": "small",
            "data": [],
            "env": {},
        },
        {
            "targets": medium_tests,
            "tags": [],
            "test_size": "medium",
            "data": [],
            "env": {},
        },
        {
            "targets": long_tests,
            "tags": ["manual"],
            "test_size": "large",
            "data": ["ic_repo_clone_for_long_tests"],
            "env": {
                "IC_REPO_SEED_TAR": "$(location ic_repo_clone_for_long_tests)",
            },
        },
    ]
]

tar(
    name = "release_controller_deps",
    srcs = ["//rs/cli:dre-embedded"],
)

tar(
    name = "commit_annotator_deps",
    srcs = [":bazelisk", ":target_determinator"],
)

py_oci_image(
    name = "oci_image",
    base = "@bazel_image_7_4_1",
    binary = ":release-controller",
    tars = [":release_controller_deps"]
)

py_oci_image(
    name = "oci_image_commit_annotator",
    base = "@bazel_image_7_4_1",
    binary = ":commit-annotator",
    tars = [":commit_annotator_deps"]
)

exports_files(["README.md"])
